buildscript {

    ext.versions = [
            'compileSdk': 28,
            'minSdk'    : 23,
            'targetSdk' : 28
    ]

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-beta04'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31'
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.0.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext {

    /**
     * Version name: <major>.<minor>.<patch>[-<verClass>]
     * For mock, dev, tst and acc <build> is attached at the end.
     */
    generateVersionName = { versionMajor, versionMinor, versionPatch, versionClassifier ->
        String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        if (versionClassifier != null) {
            versionName += "-" + versionClassifier
        }
        println "Version name: " + versionName
        return versionName
    }

    /**
     * Version code: MMMmmppbbb
     */
    generateVersionCode = { versionMajor, versionMinor, versionPatch ->
        Integer versionCode = versionMajor * 10000000 +
                versionMinor * 100000 +
                versionPatch * 1000 +
                (gitCommitCount() % 1000)
        println "Version code: " + versionCode
        return versionCode
    }

    /**
     * Returns the number of commits in the repo.
     */
    gitCommitCount = { ->
        return Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
