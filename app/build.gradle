plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id "com.github.triplet.play" version "2.6.1"
}

apply from: '../dependencies_core.gradle'
apply from: '../dependencies_test.gradle'

ext.versionMajor = 1         // API Changes, adding big new feature, redesign the App
ext.versionMinor = 0         // New features in a backwards-compatible manner
ext.versionPatch = 0         // Backwards-compatible bug fixes
ext.versionClassifier = null // Pre-releases (alpha, beta, rc, SNAPSHOT...)

android {

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = kotlinJvmTarget
    }

    signingConfigs {
        release {
            storeFile getReleaseKeyStorePath()
            storePassword getReleaseKeyStorePass()
            keyAlias getReleaseKeyAlias()
            keyPassword getReleaseKeyPass()
        }
    }

    compileSdkVersion androidConfig.compileSdkVersion
    buildToolsVersion androidConfig.buildToolsVersion

    defaultConfig {
        applicationId androidConfig.applicationId
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
        versionName generateVersionName(versionMajor, versionMinor, versionPatch, versionClassifier)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "es"
    }

    buildTypes {
        debug {
            aaptOptions {
                cruncherEnabled = false // No img optimization in debug
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled true
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            all {
                includeAndroidResources = true
                returnDefaultValues = true
                testLogging {
                    exceptionFormat "full"
                    events "started", "skipped", "passed", "failed"
                    showStandardStreams = true
                }
                reports {
                    junitXml.enabled = true
                    html.enabled = true
                }
            }
        }
    }

    dynamicFeatures = [
            ":fea_auth",
            ":fea_dashboard",
            ":fea_onboarding",
            ":fea_manage_clinics",
            ":fea_manage_workdays",
            ":fea_manage_treatments"
    ]
}

play {
    serviceAccountCredentials = getGradlePlayPublisherServiceAccCredentials()
    defaultToAppBundles = true
    track = 'internal'
    retain.artifacts = [ generateVersionCode(versionMajor, versionMinor, versionPatch) ]
}

dependencies {
    implementation project(':core')
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
apply plugin: 'com.google.gms.google-services'
