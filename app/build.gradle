plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id "com.github.triplet.play" version "2.6.1"
}

apply from: '../dependencies_core.gradle'
apply from: '../dependencies_test.gradle'

ext.versionMajor = 0         // API Changes, adding big new feature, redesign the App
ext.versionMinor = 2         // New features in a backwards-compatible manner
ext.versionPatch = 0         // Backwards-compatible bug fixes
ext.versionClassifier = null // Pre-releases (alpha, beta, rc, SNAPSHOT...)

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile getReleaseKeyStorePath()
            storePassword getReleaseKeyStorePass()
            keyAlias getReleaseKeyAlias()
            keyPassword getReleaseKeyPass()
        }
    }

    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.davidmiguel.godentist"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
        versionName generateVersionName(versionMajor, versionMinor, versionPatch, versionClassifier)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "es"
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled true
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            all {
                includeAndroidResources = true
                returnDefaultValues = true
                testLogging {
                    exceptionFormat "full"
                    events "started", "skipped", "passed", "failed"
                    showStandardStreams = true
                }
                reports {
                    junitXml.enabled = true
                    html.enabled = true
                }
            }
        }
    }

    dynamicFeatures = [
            ":fea_auth",
            ":fea_dashboard",
            ":fea_onboarding",
            ":fea_manage_clinics",
            ":fea_manage_workdays",
            ":fea_manage_treatments"
    ]
}

play {
    serviceAccountCredentials = getGradlePlayPublisherServiceAccCredentials()
    defaultToAppBundles = true
    track = 'internal'
}

dependencies {
    implementation project(':core')
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
apply plugin: 'com.google.gms.google-services'
